модуль файлы

импорт "стд::строки"
//импорт "стд::вывод"

тип Строки = []Строка

конст слеш = "/"

// Выдает разделитель имен в пути
фн разделитель*(): Строка {
    вернуть слеш
}

// Возращает путь к рабочей папке
фн путь к рабочей папке*(): Строка {
    вернуть "."
}

// Возращает путь к папке программы (папке с исполняемым файлом)
фн путь к папке программы*(): Строка {
    пусть путь = sysapi_exec_path()
    вернуть извлечь путь(путь)
}

// Собрать путь из частей
фн собрать путь*(имена: ...Строка): Строка {
    вернуть строки.соединить(слеш, имена...)
}

// Из пути извлекает путь до последнего разделителя
// "./папка/файл" => "./папка"
фн извлечь путь*(путь: Строка): Строка {

    надо длина(путь) > 0 иначе вернуть "."

    пусть с8 = путь(:Строка8)
    пусть № := длина(с8) - 1
    
    пока № >= 0 {
        надо с8[№] # '/'(:Байт) иначе прервать
        №--
    }
    
    если № < 0 {
        если путь = ".." { вернуть ".." }
        вернуть "."
    } иначе если № = 0 {
        вернуть слеш
    } 
    
    вернуть строки.извлечь(путь, 0, №)
}

// Из пути извлекает последнее имя
// "./папка/файл" => "файл"
фн извлечь последнее имя*(путь: Строка): Строка {

    надо длина(путь) > 0 иначе вернуть "."

    пусть с8 = путь(:Строка8)
    пусть № := длина(с8) - 1
    
    пока № >= 0 {
        надо с8[№] # '/'(:Байт) иначе прервать
        №--
    }
    
    если № < 0 {
        вернуть путь
    } 
    
    пусть имя = строки.извлечь(путь, №+1, длина(с8) - № - 1)
    надо длина(имя(:Строка8)) > 0 иначе вернуть "."
    
    вернуть имя
}

